show:
name:Terrain race
info:Speed competition
expdef:standard
code:~

//by MacKo, 2004, 2005

global g_restartrequest;
global g_newterrainrequest;
global g_starttime;
global g_lasttesttime, g_lasttestscore; //for detecting stagnation
global g_lastcamchange;
global g_camdeltaangle;

function onLoad()
{
  Math.seed = Math.time;
  Simulator.import("show_noevol.sim");

  var pop=Populations[0];
  pop.nnsim = 2; //will be enabled directly for each creature
  pop.death = 0;
  pop.energy = 0;
  pop.selfmask = pop.othermask = 0; //ignore collisions; could be 0x10001/0x20000 to allow eating apples, but then new apples appear in random places...

  TrackingCam.cam_chspeed = 0.1;
  TrackingCam.cam_zoom = 1.2;
  TrackingCam.setViewDir(270, 30); //270:side view
  GLDisplay.simsteps = 0;

  ShowProperties.length = 1;
  ShowProperties.difficulty = 1;
  ShowProperties_length_set();
  ShowProperties.runners = 2;
  ShowProperties_runners_set();
  GLDisplay.desiredsimspeed=100;

  Simulator.import("walking.gen");
  removeBad();
  g_restartrequest = Simulator.stepNumber + 20; //20 zeby sie utworzyly stwory... to samo w runners_set()
  g_newterrainrequest = -1;
  g_lastcamchange = Math.time;
  g_camdeltaangle = 0;
}

function putOnTheGround(cr, x, y) //just to adjust the proper "z" value in the new (x,y) world place
{
  var z = WorldMap.getHeight(x, y);
  if (World.wrldwat > z) // water here!
  {
    if (ExpProperties.creath > 0)
      z = World.wrldwat + ExpProperties.creath;
    else
      z = (1.0 + ExpProperties.creath) * World.wrldwat - z * ExpProperties.creath;
  }
  else                 // land area
  {
    if (ExpProperties.creath > 0)
      z += ExpProperties.creath;
  }
  cr.locationSetBboxLow(x, y, z - 0.999);
}

function restartRace()
{
  var i, y;
  var pop=Populations[0];
  var count = pop.size;
  if (count == 0)
    return ;
  Simulator.print("Restarting race... " + count + " runners.");
  World.wrldtyp = 2;
  for (i = 0;i < count;i++)
  {
    y = World.wrldsiz / (3 * count + 1) * (3.0 * i + 2);
    var cr = pop[i];
    cr.rotate(0,0,GenePools[0].findGenotype(cr.geno).data->walking_direction); //use rotateAbs instead, when it is available
    putOnTheGround(cr, World.wrldsiz - 10.0 - cr.bboxSize.x / 2, y - cr.bboxSize.y / 2);
    cr.data->target_x = 10.0 - cr.bboxSize.x / 2;
    cr.nnenabled = 1; //bring to life
    cr = Populations[1][i]; //food "target indicator"
    putOnTheGround(cr, 10.0 - cr.bboxSize.x / 2, y - cr.bboxSize.y / 2);
  }
  g_starttime = Math.time;
  g_lasttesttime = g_starttime;
  g_lasttestscore = 99999;
}

function status()
{
  var i, best = 0, cr, s;
  var pop=Populations[0];
  if (pop.size == 0)
    return "no runners yet...";
  for (i = 1;i < pop.size;i++)
    if (pop[i].bboxLow.x < pop[best].bboxLow.x)
      best = i;
  var lane = pop.size - best;
  cr = pop[best];
  i = int(cr.bboxLow.x - cr.data->target_x);
  s = cr.name + " leads on lane #" + lane + ", " + i + "m to finish";
  if (i < 4)
    s = cr.name + " on lane #" + lane + " finishing...";
  if (i < 1)
  {
    s = cr.name + " on lane #" + lane + " wins!!!";
    g_newterrainrequest = Simulator.stepNumber + 95;
  }
  //add more nice messages, like "xxx gives up?" etc.

  var t = int(Math.time - g_starttime);
  if (g_starttime)
    s = ("Elapsed time: %d:%02d" % (t/60) % (t%60)) + "\n" + s;

  if (Math.time - g_lasttesttime > 60) //stagnation detection - every minute
  {
    if (g_lasttestscore - i < 2)
    {
      s = "This race seems too difficult... let's start another one!";
      g_newterrainrequest = Simulator.stepNumber + 295;
    }
    g_lasttesttime = Math.time;
    g_lasttestscore = i;
  }
  return s;
}

function setCam()
{
  if (Math.time - g_lastcamchange > 20)
  {
    g_camdeltaangle = (Math.rnd01 - 0.5) / 2;
    //add also: TrackingCam.setTarget(winner), TrackingCam.setTarget(looser), funny comments, etc.
    g_lastcamchange = Math.time;
  }
  TrackingCam.cam_dir += g_camdeltaangle; //270:side view, 0=360:front view
  if (TrackingCam.cam_dir < 270 || TrackingCam.cam_dir > 359)
    g_camdeltaangle = 0;
}

function onSimStep()
{
  if (Simulator.stepNumber == g_restartrequest)
    ShowProperties_restart_race_call();
  if (Simulator.stepNumber == g_newterrainrequest)
    ShowProperties_length_set();
  if ((Simulator.stepNumber % 100) == 0 &&
      !(g_newterrainrequest > Simulator.stepNumber || g_restartrequest > Simulator.stepNumber)) //no pending requests
    GLDisplay.banner = status();
}

function onShowStep()
{
    setCam();
}

function removeBad()
{
  var allowed = [56, 58, 60, 77, 80, 83, 287, 307]; //only those who can walk straight
  var angles = [Math.pi, 0, 0, Math.pi, Math.pi, 0, 0, 0]; //rotation needed for some of them
  var i, j;
  
  var pool=GenePools[0];
  for (i = pool.size - 1;i >= 0;i--)
  {
    var g=pool[i];
    j = allowed.find(g.num);
    if (j<0)
    {
      Simulator.print("\"" + g.name + "\" can't run straight, dismissed.");
      g.delete();
    } else g.data->walking_direction = angles[j];
  }
}

function makeTrackSegment(currH, nextH)
{
  var d = nextH - currH;
  var s = "" + currH;
  s += " " + (currH + d * 1 / 4);
  s += " " + (currH + d * 3 / 4);
  s += " " + nextH;
  return s;
}

function makeTrack()
{
  var height = 1.0, track, i;
  track = makeTrackSegment(height, height) + " ";
  var diffic = ShowProperties.difficulty * 3 + 1;
  for (i = 0;i < ShowProperties.length + 1;i++)
  {
    var prevheight = height;
    height += Math.rnd01 * diffic - 0.5 * diffic;
    track += makeTrackSegment(prevheight, height) + " ";
  }
  track += makeTrackSegment(height, height);
  return track;
}

function ShowProperties_length_set, ShowProperties_difficulty_set, ShowProperties_runners_set()
{
  Populations[0].clear();
  Populations[1].clear();
  ExpProperties.MaxCreated = [2, 3, 5, 7, 9][ShowProperties.runners];
  ExpProperties.feed = ExpProperties.MaxCreated;
  World.wrldtyp = 0; //flat for some time to allow new creatures be revived on a flat plane
  g_restartrequest = Simulator.stepNumber + 20;

  var l = makeTrack(), l2 = ""; //l2 is the separating (a little higher) lane
  var i, k = "";
  var v = String.split2(l, " ");
  l = "";
  for (i = v.size - 1;i >= 0;i--) //reversing order
  {
    l += "" + v[i] + " ";
    l2 += "" + (1.5 + float(v[i])) + " "; //higher: +1.5
  }
  for (i = 0;i < ExpProperties.MaxCreated;i++)
    k += l2 + "\n" + l + "\n" + l + "\n";
  //  Simulator.print("\n"+l+"\n"+l2);
  World.wrldmap = "M " + 4 * (3 + ShowProperties.length) + " " + (ExpProperties.MaxCreated * 3 + 1) + " " + k + l2;
  World.wrldsiz = 6.0 * 4 * (3 + ShowProperties.length);
  World.wrldwat = 0;
  World.wrldbnd = 1;
}

function ShowProperties_restart_race_call()
{
  restartRace();
}

~

property:
id:restart_race
name:Restart race
type:p

property:
id:runners
name:Number of runners
type:d 0 4 ~2~3~5~7~9

property:
id:length
name:Race length
type:d 0 2 ~Short~Medium~Long

property:
id:difficulty
name:Race difficulty
type:d 0 2 ~Easy~Medium~Hard
