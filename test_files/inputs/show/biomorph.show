show:
name:Biomorph
info:~
User-driven evolution. Choose a creature and double-click it to create its offspring. Eight creatures are mutated from the one in the middle.
~
expdef:standard
code:~

global g_anim;
global g_animspeed;
global g_animtime;
global g_selcreature;
global g_selx;
global g_sely;
global g_selz;
global g_lasttime;
global g_speed;
global g_frames;

function onLoad()
{
  Math.seed = Math.time;
  g_speed = 1.0;
  g_frames = 0;
  g_lasttime = Math.time;
  World.wrldtyp = 1;
  World.wrldmap = "m 3 3 676787676";
  World.wrldsiz = 20.0;
  World.wrldbnd = 1;
  GLDisplay.simsteps = 0;
  ShowProperties.differ = 1;
  TrackingCam.cam_chspeed = 0.1;
  TrackingCam.cam_zoom = 1.3;
  ShowProperties_restart_f1_call();
  g_animspeed = 0.002;
}

function restartWithSimplest(format)
{
	var simplest=GenMan.getSimplest(format);
  CreatureSelection.clear();
  var pop=Populations[0];
  pop.clear();
  var cr = pop.add(simplest.genotype);
  cr.name = "Simplest geno in f"+format+" format";
  cr.info = "The origin";
  if (cr == null)
  {
    CLI.println("Initial genotype is invalid!");
    return ;
  }
  cr.locationSetBboxLow((World.wrldsiz -cr.bboxSize.x) / 2, (World.wrldsiz - cr.bboxSize.y) / 2, 3);
  updateBanner();
  g_anim = 0;
  breed();
}

function createMutant(threshold) //same function in mutation.show, biomorph.show and AL-biomorph.show :/
{
  var cr=Populations[0][0];
  var mut_count = 0;
  var distance;
  var original_g = cr.geno; // previous genotype
  var new_g = original_g;
  var max_retry = 20;
  if (original_g.format == "F") //'F' format for which 1) structural similary is not very useful and 2) conversion fF->f0 takes a lot of time for multiple-Part creatures, so let's not do this many times here
  	max_retry = 5;
  for(var retry=0; retry<max_retry; retry++)
  {
    new_g=GenMan.mutate(new_g);
    mut_count++;
    if (!new_g.is_valid)
    {
      new_g=original_g;
      mut_count = 0;
      continue;
    }
    //	CLI.println(original_g.genotype+" ... "+new_g.genotype);
    if (threshold < 0.001) //no threshold
      break;
    distance = ModelSimilarity.evaluateDistance(original_g, new_g);
    //	CLI.println("distance="+distance);
    if (distance >= threshold)
      break;
  }
  if (mut_count>1) //when there was a sequence of many mutations, we don't know how many % of genotype mutated, so the information in new_g.info is useless (it only concerns the last mutation in the sequence, and creature names in the sequence are not auto-generated).
    new_g.info="'"+original_g.name+"' after "+mut_count+" mutations";
  if (!new_g.is_valid)
  {
    CLI.println("invalid mutant!");
  }
  else
  {
    Populations[0].add(new_g);
  }
}

function breed()
{
  var i;
  for (i = 0;i < 8;i++)
    createMutant([0.0, 0.5, 2.0, 10.0][ShowProperties.differ]);
  
  //adjust world size to fit creatures:
  var size=0.0;
  var c,count=9;
  var pop=Populations[0];
  for (c = 0;c < count;c++)
  {
    var cr = pop[c];
    size+=Math.max(cr.bboxSize.x,cr.bboxSize.y); //shape=0 parts have size_xyz=1.0 (even though they are visually smaller)
    //Simulator.print(""+cr.bboxSize.x+" "+cr.bboxSize.y+" "+cr.bboxSize.z);
  }
  var newsize=(2.0+3.0)*(size/count); //(2 margin tiles + 3 world blocks)*(average creature size)
  if (newsize>1.1*World.wrldsiz || newsize<World.wrldsiz*0.7) //resize the world only when a significant change in size is required
    World.wrldsiz=newsize;
  //center creatures in the middle of the (just resized) world
  for (c = 0;c < count;c++)
  {
    var cr = pop[c];
    cr.locationSetBboxLow((World.wrldsiz - cr.bboxSize.x) / 2, (World.wrldsiz - cr.bboxSize.y) / 2, 3);
  }
    
  g_anim = 1;
  g_animtime = 0.0;
}

function updateBanner()
{
  var t = "";
  var pop=Populations[0];
  if (pop.size > 0)
  {
    var cr = pop[0];
    t += "Current creature '" + cr.name + "' has a genotype\n" + cr.genotype + "\nwhich is " + cr.info + ".";
  }
  GLDisplay.banner = t;
}

function explodeStep()
{
  g_animtime = g_animtime + g_animspeed;
  if (g_animtime > 1.0)
  {
    g_animtime = 1.0;
    g_anim = 0;
  }

  var t1 = g_animtime, t2 = 1.0 - g_animtime;
  var i, j, c, cr;
  var tx, ty, tz, x, y, z;
  var pop=Populations[0];
  c = 0;
  for (i = -1;i <= 1;i++)
    for (j = -1;j <= 1;j++)
    {
      if ((i == 0) && (j == 0))
        continue;
      c++;
      tx = World.wrldsiz * (0.5 + 0.2 * i);
      ty = World.wrldsiz * (0.5 + 0.2 * j);
      if ((i == 0) || (j == 0))
        tz = 2.0;
      else
        tz = 1.0;
      tz += t1 * t2 * 30.0;
      cr = pop[c];
      x = (t2 * (World.wrldsiz / 2)) + (t1 * tx) - (cr.bboxSize.x / 2);
      y = (t2 * (World.wrldsiz / 2)) + (t1 * ty) - (cr.bboxSize.y / 2);
      z = t2 * 3 + t1 * tz;
      cr.locationSetBboxLow(x, y, z);
    }
}

function selectionStep()
{
  g_animtime = g_animtime + g_animspeed;
  if (g_animtime > 1.0)
  {
    g_animtime = 1.0;
    g_anim = 0;
  }

  var t1 = g_animtime, t2 = 1.0 - g_animtime;
  var i, j, c, cr;
  var tx, ty, tz, x, y, z;
  var pop=Populations[0];
  for (c = 0;c <= 8;c++)
  {
    cr = pop[c];
    if (cr == g_selcreature)
    {
      x = (t1 * ((World.wrldsiz - cr.bboxSize.x) / 2)) + (t2 * g_selx);
      y = (t1 * ((World.wrldsiz - cr.bboxSize.y) / 2)) + (t2 * g_sely);
      z = t1 * 3 + t2 * g_selz;
      cr.locationSetBboxLow(x, y, z);
    }
    else
      cr.locationMoveBy(0, 0, g_animspeed*cr.bboxSize.z* -1.0);
  }

  if (g_anim == 0)
  {
    for (c = 0;c < pop.size;c++)
    {
      cr = pop[c];
      if (cr != g_selcreature)
      {
        pop.delete(c);
        c--;
      }
    }
    updateBanner();
    breed();
  }
}

function onShowStep()
{
  TrackingCam.cam_dir += g_animspeed;
  switch (g_anim)
  {
  case 1:
    explodeStep();
    break;
  case 2:
    selectionStep();
    break;
  }
  g_frames++;
  var t = Math.time;
  var dt = Math.time - g_lasttime;
  if ((dt > 1.0) && (g_frames > 5))
  {
    dt = dt / g_frames;
    if (dt < 0.001)
      dt = 0.001;
    else if (dt > 1.0)
      dt = 1.0;
    //	CLI.println("adjusted anim speed for "+1.0/dt+" fps");
    g_animspeed = (g_animspeed + g_speed * dt) / 2.0;
    g_lasttime = t;
    g_frames = 0;
  }
}

function onSelectionChange()
{
  if (CreatureSelection.count > 0)
  {
    if (g_anim == 0)
    {
      g_anim = 2;
      g_animtime = 0.0;
      g_selcreature = CreatureSelection.get(0);
      CLI.println("selected:\n" + g_selcreature.genotype);
      g_selx = g_selcreature.bboxLow.x;
      g_sely = g_selcreature.bboxLow.y;
      g_selz = g_selcreature.bboxLow.z;
    }
    CreatureSelection.clear();
  }
  updateBanner();
}

function ShowProperties_restart_f1_call() { restartWithSimplest("1"); }
function ShowProperties_restart_f9_call() { restartWithSimplest("9"); }
function ShowProperties_restart_f4_call() { restartWithSimplest("4"); }
function ShowProperties_restart_f0_call() { restartWithSimplest("0"); }
function ShowProperties_restart_fF_call() { restartWithSimplest("F"); }

~

property:
id:restart_f1
name:Restart with a simple f1 genotype (recurrent encoding)
type:p

property:
id:restart_f9
name:Restart with a simple f9 genotype (perpendicular encoding)
type:p

property:
id:restart_f4
name:Restart with a simple f4 genotype (developmental encoding)
type:p

property:
id:restart_f0
name:Restart with a simple f0 genotype (low-level encoding)
type:p

property:
id:restart_fF
name:Restart with a simple fF genotype (Foraminifera encoding)
type:p

property:
id:differ
name:Mutant variation
type:d 0 3 ~Single mutation~Forced: small~Forced: medium~Forced: big

